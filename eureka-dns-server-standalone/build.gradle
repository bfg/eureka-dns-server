//
// build.gradle
//

buildscript {
  // shadow plugin 4.0.4 fails with palantir's docker plugin
  // see: https://github.com/johnrengelman/shadow/pull/460
  dependencies {
    classpath files("libs/shadow-5.0.0-SNAPSHOT.jar")
  }
}

plugins {
  id "application"
  id "com.gorylenko.gradle-git-properties"  version "2.0.0"

  // shadow plugin 4.0.4 fails with palantir's docker plugin
  // see: https://github.com/johnrengelman/shadow/pull/460
  //
  // id "com.github.johnrengelman.shadow"      version "4.0.4"

  // docker support
  id "com.palantir.git-version"             version "0.12.0-rc2"
  id "com.palantir.docker"                  version "0.21.0"
}

apply plugin: "com.github.johnrengelman.shadow"

dependencies {
  compileOnly       "org.projectlombok:lombok"

  compile           "javax.inject:javax.inject:1"
  compile           "org.slf4j:jcl-over-slf4j"

  compile           "info.picocli:picocli:3.9.3"
  compile           "ch.qos.logback:logback-classic"
  compile           "io.netty:netty-transport-native-epoll:${nettyVersion}:linux-x86_64"

  compile           project(":eureka-dns-server")
}

gitProperties {
  dateFormat          = "yyyy-MM-dd' 'HH:mm:ssZ"
  dateFormatTimeZone  = "UTC"
  gitPropertiesDir    = "${project.buildDir}/resources/main"

  keys                = [ 'git.build.version', 'git.commit.id', 'git.commit.id.abbrev','git.commit.time' ]
}

application {
  mainClassName = "com.github.bfg.eureka.dns.standalone.EurekaDnsServerCli"
}

// disable creation of distribution archives
distZip.enabled = distTar.enabled = shadowDistTar.enabled = shadowDistZip.enabled = false

// don't publish any artifacts to maven repo.
tasks.withType(PublishToMavenRepository).all { it.onlyIf { false } }

// shadow uberjar support
shadowJar {
  baseName = "eureka-dns-server"
  classifier = null

  mergeServiceFiles()
}
build.finalizedBy shadowJar

// docker support
docker {
  name        "gracnar/eureka-dns-server:${version}"
  dockerfile  file("Dockerfile")

  def artifact = "eureka-dns-server-${version}.jar"
  files       "build/libs/${artifact}",
                 "${rootProject.projectDir}/scripts/java-service-starter"
  buildArgs   "serviceJar": "${artifact}"
}

// vim:shiftwidth=2 softtabstop=2 expandtab
// EOF
