//
// build.gradle
//

plugins {
  id "application"
  id "com.gorylenko.gradle-git-properties"  version "2.0.0"
  id "com.github.johnrengelman.shadow"      version "5.0.0"

  // docker support
  id "com.palantir.git-version"             version "0.12.0-rc2"
  id "com.palantir.docker"                  version "0.21.0"
}

apply plugin: "com.github.johnrengelman.shadow"

dependencies {
  compileOnly         "org.projectlombok:lombok"

  // main project artifact
  implementation      project(":eureka-dns-server")

  implementation      "javax.inject:javax.inject:1"
  implementation      "com.netflix.eureka:eureka-client"

  implementation      "org.slf4j:jcl-over-slf4j"

  implementation      "info.picocli:picocli:3.9.5"
  implementation      "ch.qos.logback:logback-classic"
  implementation      "io.netty:netty-transport-native-epoll:${nettyVersion}:linux-x86_64"

  // main project test artifacts
  testCompile       project(path: ":eureka-dns-server", configuration: "testOutput")
}

gitProperties {
  dateFormat          = "yyyy-MM-dd' 'HH:mm:ssZ"
  dateFormatTimeZone  = "UTC"
  gitPropertiesDir    = "${project.buildDir}/resources/main"

  keys                = [ 'git.build.version', 'git.commit.id', 'git.commit.id.abbrev','git.commit.time' ]
}

application {
  mainClassName = "com.github.bfg.eureka.dns.standalone.EurekaDnsServerCli"
}

// disable creation of distribution archives
distZip.enabled = distTar.enabled = shadowDistTar.enabled = shadowDistZip.enabled = false

// don't publish any artifacts to maven repo.
tasks.withType(PublishToMavenRepository).all { it.onlyIf { false } }

// shadow uberjar support
shadowJar {
  baseName = "eureka-dns-server"
  classifier = null

  mergeServiceFiles()
}
build.finalizedBy shadowJar

// docker support
docker {
  name        "gracnar/eureka-dns-server:${version}"
  dockerfile  file("Dockerfile")

  def artifact = "eureka-dns-server-${version}.jar"
  files       "build/libs/${artifact}",
                 "${rootProject.projectDir}/scripts/java-service-starter"
  buildArgs   "serviceJar": "${artifact}"
}

// vim:shiftwidth=2 softtabstop=2 expandtab
// EOF
